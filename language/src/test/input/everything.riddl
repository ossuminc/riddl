// #everything
// RIDDL is about defining domains so ...
domain Everything by author Reid is {

  author Reid is { name: "Reid" email: "reid@ossum.biz" }

  type SomeType is String
  command DoAThing is { thingField: Integer }

  // Channels are defined to flow messages between entities. They are defined
  // in domain scope because they can cross contexts too.

  plant APlant is {
    source Source is { outlet Commands is type DoAThing } described by "Data Source"
    sink Sink is { inlet Commands is type DoAThing } explained as "Data Sink"
    pipe AChannel is { transmit type DoAThing } explained as "A Channel"
    joint input is outlet Source.Commands to pipe AChannel
    joint output is inlet Sink.Commands from pipe AChannel
  }

  actor Author is "human" briefly "A scoundrel"

  story WritingABook is {
    actor ^Author wants "to edit on the screen" so that "he can revise content more easily"
    case primary is { ??? }
    example one {
      given "I need to write a book"
      when "I am writing the book"
      then "I can easily type words on the screen instead of using a pen"
    } described by "nothing"
    example two {
      given "I need to edit a previously written book"
      when "I am revising the book"
      then "I can erase and re-type words on the screen"
    }
  } described as "A simple authoring story"

  // Domains are composed of bounded contexts so ...
  context full is {
    // Contexts can contain many kinds of definitions

    // 8 pre-defined types, shown as re-names
    type str is String             // Define str as a String
    type num is Number             // Define num as a Number
    type boo is Boolean            // Define boo as a Boolean
    type ident is Id(Something)    // Define ident as an Id
    type dat is Date               // Define dat as a Date
    type tim is Time               // Define tim as a Time
    type stamp is TimeStamp        // Define stamp as a TimeStamp
    type url is URL

    // Enumerations have a value chosen from a list of identifiers
    type PeachType is { a: Integer }
    type enum is any of { Apple Pear Peach(23)  Persimmon(24) }

    // Alternations select one type from a list of types
    type alt is one of { enum or stamp or url }

    // Aggregations combine several types and give each a field name identifier
    type agg is {
      key: num,
      id: ident,
      time is TimeStamp
    }

    // Types can have cardinality requirements similar to regular expressions
    type oneOrMore is many agg
    type zeroOrMore is agg*
    type optional is agg?

    // Commands, Events, Queries and Results are defined in terms of some
    // type, previously defined. Commands yield events. Queries yield results.

    command ACommand is {}

    // Entities are the main objects that contexts define. They can be
    // transient (memory only) or aggregate (they consume commands and queries)
    entity Something is {
      options (aggregate, transient)

      type somethingDate is Date

      event Inebriated is {}

      state someState is {
        fields { field:  SomeType }
        handler foo is {
          on command ACommand {
            example only {
              given "Something arrives "
              when misc()
              then tell event Inebriated() to entity Everything.full.SomeOtherThing
            }
          }
        }
      }

      function whenUnderTheInfluence is {
        requires {n:Nothing}
        returns: {b: Boolean}
      }
    }

    entity SomeOtherThing is {
      type ItHappened is event { field: String }
      state otherThingState is {
        fields { field: String }
        handler fee is {
          on event ItHappened {
            example only {
              then set SomeOtherThing.otherThingState.field to @ItHappened.field
            }
          }
        }
      }
    }
  }
}

// #everything
